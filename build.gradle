apply plugin: 'maven-publish'
apply plugin: 'signing'

allprojects {
    group = GROUP
    version = VERSION
}

subprojects {
    buildDir = rootProject.file("build/${project.name}")

    repositories {
        mavenCentral()

        flatDir {
            dirs rootProject.file('libs')
        }
    }

    publishing {
        repositories {
            mavenLocal()
        }
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }


task flashlibSources(type: Jar, dependsOn: subprojects.classes) {
    baseName = 'flashlib'
    destinationDir = rootProject.buildDir
    classifier = 'sources'

    from files(subprojects.collect {
        it.sourceSets.main.allSource
    })
}

task flashlibJavadoc(type: Jar, dependsOn: subprojects.javadoc) {
    baseName = 'flashlib'
    destinationDir = rootProject.buildDir
    classifier = 'javadoc'

    from files(subprojects.collect {
        it.javadoc.destinationDir
    })
}

task flashlib(type: Jar, dependsOn: subprojects.build) {
    dependsOn flashlibSources
    dependsOn flashlibJavadoc

    baseName = 'flashlib'
    destinationDir = rootProject.buildDir

    from files(subprojects.collect {
        it.sourceSets.main.output
    })
}

task flashlibAll(type: Jar, dependsOn: flashlib) {
    dependsOn flashlibSources
    dependsOn flashlibJavadoc

    baseName = 'flashlib-all'
    destinationDir = rootProject.buildDir

    from files(subprojects.collect {
        it.sourceSets.main.output
    })
    from files(subprojects.collect {
        it.sourceSets.main.allSource
    })
    from files(subprojects.collect {
        it.javadoc.destinationDir
    })
}

artifacts {
    archives flashlib
    archives flashlibSources
    archives flashlibJavadoc
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenFlashlib(MavenPublication) {
            artifactId = 'flashlib'

            artifact flashlib
            artifact flashlibJavadoc
            artifact flashlibSources
        }
    }

    repositories {
        maven {
            name = 'Nexus Staging'
            url = NEXUS_RELEASE_REPOSITORY_URL

            credentials {
                username ''
                password ''
            }
        }

        maven {
            name = 'Nexus Snapshot'
            url = NEXUS_SNAPSHOT_REPOSITORY_URL

            credentials {
                username ''
                password ''
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'FlashLib'
            url 'https://github.com/Flash3388/FlashLib'
            description 'Robotics development framework'

            licenses {
                license {
                    name 'BSD 3-Clause License'
                    url 'https://opensource.org/licenses/BSD-3-Clause'
                }
            }

            developers {
                developer {
                    id 'tomtzook'
                    name 'Tom Tzook'
                    email 'tomtzook@gmail.com'
                }
            }

            scm {
                connection 'scm:git:git://github.com/Flash3388/FlashLib.git'
                developerConnection 'scm:git:ssh://github.com/Flash3388/FlashLib.git'
                connection 'https://github.com/Flash3388/FlashLib'
            }
        }
    }
}