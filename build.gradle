apply plugin: 'maven'

allprojects {
    group = 'edu.flash3388.flashlib'
    version = '1.3'
}

subprojects {
    buildDir = rootProject.file("build/${project.name}")

    repositories {
        mavenCentral()

        flatDir {
            dirs rootProject.file('libs')
        }
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

task allFlashlib(type: Jar, dependsOn: subprojects.assemble) {
    baseName = 'flashlib'
    destinationDir = rootProject.buildDir

    from files(subprojects.collect {
        it.sourceSets.main.output
    })
}

task allFlashlibSources(type: Jar, dependsOn: subprojects.classes) {
    baseName = 'flashlib'
    destinationDir = rootProject.buildDir
    classifier = 'sources'

    from files(subprojects.collect {
        it.sourceSets.main.allSource
    })
}

task allFlashlibJavadoc(type: Jar, dependsOn: subprojects.javadoc) {
    baseName = 'flashlib'
    destinationDir = rootProject.buildDir
    classifier = 'javadoc'

    from files(subprojects.collect {
        it.javadoc.destinationDir
    })
}

artifacts {
    archives allFlashlib
    archives allFlashlibSources
    archives allFlashlibJavadoc
}
