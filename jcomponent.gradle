apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: rootProject.file('dependencies.gradle')

sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile slf4jApi()

    testCompile junit()
    testCompile mockitoCore()
    testCompile hamcrest()
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces true
    }
}

jar {
    baseName project.name
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName project.name
    classifier = 'sources'

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName project.name
    classifier = 'javadoc'

    from javadoc.destinationDir
}

build.finalizedBy sourcesJar, javadocJar

publishing {
    publications {
        mavenFlashlibComponent(MavenPublication) {
            artifactId = project.name

            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = project.name.capitalize()
                description = "Robotics development framework - ${project.name.capitalize()} component".toString()
                url = 'https://github.com/Flash3388/FlashLib'

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Flash3388/FlashLib.git'
                    developerConnection = 'scm:git:ssh://github.com/Flash3388/FlashLib.git'
                    url = 'https://github.com/Flash3388/FlashLib'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenFlashlibComponent
}